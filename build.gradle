plugins {
    id 'java'
    id 'application'
}

compileJava {
    sourceCompatibility = project.supportedJavaVersion
    targetCompatibility = project.supportedJavaVersion
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

application {
    mainClassName = 'com.linuxforhealth.connect.App'
}

dependencies {
    // camel libraries
    implementation group: 'org.apache.camel', name: 'camel-core', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-fhir', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-dataformat', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-exec', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-mail', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-kafka', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-http', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-jackson', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-jasypt', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-jetty', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-main', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-nats', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-rest', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-support', version: project.camelVersion

    // Java utils (base64 encoding, OS type)
    implementation group: 'commons-codec', name: 'commons-codec', version: project.commonsVersion
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: project.commonsLang3Version

    // data format support
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.jacksonVersion
    implementation group: 'org.json', name: 'json', version: project.jsonVersion

    // logging
    implementation group: 'ch.qos.logback', name: 'logback-core', version: project.logbackVersion
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: project.logbackVersion
    implementation group: 'org.slf4j', name: 'slf4j-api', version: project.slf4jVersion

    // HL7-MLLP processing
    implementation group: 'org.apache.camel', name: 'camel-netty', version: project.camelVersion
    implementation group: 'org.apache.camel', name: 'camel-hl7', version: project.camelVersion
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v21', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v22', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v23', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v231', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v24', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v25', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v251', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v26', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v27', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v28', version: project.hapiHl7Version
    implementation group: 'ca.uhn.hapi', name: 'hapi-structures-v281', version: project.hapiHl7Version

    // FHIR Processing
    implementation group: 'ca.uhn.hapi.fhir', name: 'hapi-fhir-base', version: project.hapiFhirVersion
    implementation group: 'ca.uhn.hapi.fhir', name: 'hapi-fhir-structures-r4', version: project.hapiFhirVersion
    implementation group: 'ca.uhn.hapi.fhir', name: 'hapi-fhir-client', version: project.hapiFhirVersion
    implementation group: 'ca.uhn.hapi.fhir', name: 'hapi-fhir-structures-dstu3', version: project.hapiFhirVersion
    implementation group: 'ca.uhn.hapi.fhir', name: 'hapi-fhir-converter', version: project.hapiFhirConvertorVersion

    // testing
    testImplementation group: 'org.apache.camel', name: 'camel-test-junit5', version: project.camelVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: project.junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.junitVersion
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClassName,
                'Class-Path': configurations.runtimeClasspath.collect { 'libs/' + it.getName() }.join(' '),
                'Implementation-Title': 'com.linuxforhealth.com',
                'Implementation-Version': project.projectVersion,
                'Implementation-Vendor': 'Linux For Health'
        )
    }
}

task archiveDependencies(type: Tar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveFileName = 'linux-for-health-connect-dependencies.tar'
    destinationDirectory = file('build/libs')
    outputs.file(archiveFile)

    from configurations.compileClasspath.each {
        from it.getPath()
    }
}

build.dependsOn archiveDependencies

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        events = ["failed"]
    }
}